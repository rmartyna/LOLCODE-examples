HAI 1.4

	CAN HAS STDIO?
	CAN HAS STDLIB?


	HOW IZ I GET_LENGTH
		VISIBLE "Enter array length:: "
		I HAS A LENGTH
		GIMMEH LENGTH
		LENGTH IS NOW A NUMBR
		FOUND YR LENGTH
	IF U SAY SO

	HOW IZ I CREATE_ARRAY_INPUT YR LENGTH
		I HAS A ARRAY ITZ A BUKKIT
		IM IN YR GET_VALUES UPPIN YR INDEX TIL BOTH SAEM INDEX AN LENGTH
			VISIBLE "Enter value for index :{INDEX}:: "
			I HAS A VALUE
			GIMMEH VALUE
			I HAS A VAR_NAME ITZ SMOOSH "I" AN INDEX MKAY
			ARRAY HAS A SRS VAR_NAME ITZ VALUE
		IM OUTTA YR GET_VALUES
		FOUND YR ARRAY
	IF U SAY SO

	HOW IZ I CREATE_ARRAY_RANDOM YR LENGTH
		I HAS A ARRAY ITZ A BUKKIT
		IM IN YR GET_VALUES UPPIN YR INDEX TIL BOTH SAEM INDEX AN LENGTH
			I HAS A VALUE ITZ I IZ STDLIB'Z BLOW YR 10000 MKAY
			I HAS A VAR_NAME ITZ SMOOSH "I" AN INDEX MKAY
			ARRAY HAS A SRS VAR_NAME ITZ VALUE
		IM OUTTA YR GET_VALUES
		FOUND YR ARRAY
	IF U SAY SO

	HOW IZ I CREATE_ARRAY_DEFAULT
		I HAS A ARRAY ITZ A BUKKIT
		ARRAY HAS A I0 ITZ 64
		ARRAY HAS A I1 ITZ 25
		ARRAY HAS A I2 ITZ 12
		ARRAY HAS A I3 ITZ 22
		ARRAY HAS A I4 ITZ 11
		FOUND YR ARRAY
	IF U SAY SO

	HOW IZ I PRINTARRAY YR ARRAY AN YR LENGTH
		I HAS A INDEX ITZ 0
		IM IN YR PRINTLOOP UPPIN YR INDEX TIL BOTH SAEM INDEX AN LENGTH
			I HAS A INDEX_N ITZ SMOOSH "I" AN INDEX MKAY
			I HAS A VALUE ITZ ARRAY'Z SRS INDEX_N
			VISIBLE "Index :{INDEX}: :{VALUE}"
		IM OUTTA YR PRINTLOOP
	IF U SAY SO

	HOW IZ I SORT_ARRAY YR ARRAY AN YR LENGTH
		
		IM IN YR FIRST_LOOP UPPIN YR INDEX_FIRST TIL BOTH SAEM INDEX_FIRST AN LENGTH

			I HAS A FIRST_INDEX_N ITZ SMOOSH "I" AN INDEX_FIRST MKAY
			I HAS A MIN_VAL ITZ ARRAY'Z SRS FIRST_INDEX_N
			I HAS A MIN_INDEX ITZ FIRST_INDEX_N

			I HAS A INDEX_SECOND ITZ INDEX_FIRST
			IM IN YR SECOND_LOOP
				I HAS A SECOND_INDEX_N ITZ SMOOSH "I" AN INDEX_SECOND MKAY
				I HAS A CURRENT_VAL ITZ ARRAY'Z SRS SECOND_INDEX_N

				I HAS A SMALLER_VAL ITZ SMALLR OF MIN_VAL AN CURRENT_VAL
				DIFFRINT MAEK MIN_VAL A NUMBR AN MAEK SMALLER_VAL A NUMBR, O RLY?
					YA RLY
						MIN_VAL R CURRENT_VAL
						MIN_INDEX R SECOND_INDEX_N
				OIC
				
				BOTH SAEM INDEX_SECOND AN DIFF OF LENGTH AN 1, O RLY?
					YA RLY
						GTFO
					NO WAI
						INDEX_SECOND R SUM OF INDEX_SECOND AN 1
				OIC
			IM OUTTA YR SECOND_LOOP
			
			I HAS A SWAP_VAL ITZ ARRAY'Z SRS FIRST_INDEX_N
			ARRAY'Z SRS FIRST_INDEX_N R MIN_VAL
			ARRAY'Z SRS MIN_INDEX R SWAP_VAL

		IM OUTTA YR FIRST_LOOP

		FOUND YR ARRAY
	IF U SAY SO


	I IZ STDLIB'Z BLOW YR 10000 MKAY


	I HAS A LENGTH ITZ I IZ GET_LENGTH MKAY
	I HAS A ARRAY ITZ I IZ CREATE_ARRAY_RANDOM YR LENGTH MKAY

	BTW I HAS A SORTEDARRAY ITZ ARRAY
	I HAS A SORTEDARRAY ITZ I IZ SORT_ARRAY YR ARRAY AN YR LENGTH MKAY

	I IZ PRINTARRAY YR SORTEDARRAY AN YR LENGTH MKAY

KTHXBYE